def nil class Nil()()
def cons class Cons(car, cdr)

infix right 5 : cons

def listToString(list) match list {
    Nil() "nil"
    Cons(car, cdr) car.toString() + " : " + listToString(cdr)
}

def Nil.toString() listToString(this)
def Cons.toString() listToString(this)

def count(i) {
    def go(i, acc) 
        {i == 0}.if(acc, go(i - 1, i : acc))
    go(i, nil)
}

def forEach(list, f) match list {
    Nil() {}
    Cons(car, cdr) {
        f(car)
        forEach(cdr, f)
    }
}

def map(list, f) match list {
    Nil() nil
    Cons(car, cdr) f(car) : map(cdr, f)
}

def fold(list, acc, f) match list {
    Nil() acc
    Cons(car, cdr) fold(cdr, f(car, acc), f)
}

def filter(list, f) match list {
    Nil() nil
    Cons(car, cdr) f(car).if(
        car : filter(cdr, f),
        filter(cdr, f)
    )
}
